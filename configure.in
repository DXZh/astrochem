#  configure.in - Autoconf file for Astrochem
#
#  Copyright (c) 2006-2012 Sebastien Maret
# 
#  This file is part of Astrochem.
#
#  Astrochem is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published
#  by the Free Software Foundation, either version 3 of the License,
#  or (at your option) any later version.
#
#  Astrochem is distributed in the hope that it will be useful, but
#  WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
#  General Public License for more details.
# 
#  You should have received a copy of the GNU General Public License
#  along with Astrochem.  If not, see <http://www.gnu.org/licenses/>.

AC_PREREQ(2.59)
AC_INIT(astrochem, 0.4, sebastien.maret@obs.ujf-grenoble.fr)
RELEASE_DATE="June 3, 2011"
AC_SUBST([RELEASE_DATE])
AM_INIT_AUTOMAKE(astrochem, 0.4)
AC_CONFIG_SRCDIR([config.h.in])
AC_CONFIG_HEADER([config.h])

# Set package options
AC_ARG_ENABLE([openmp],	AC_HELP_STRING([--enable-openmp], [enable openmp (default is no)]))

# Check for programs
AC_PROG_CC
AC_PROG_F77
AC_F77_WRAPPERS
AM_PATH_PYTHON([2.4],, AC_MSG_ERROR(could not find Python.))
AC_CHECK_PROGS(PDFLATEX, pdflatex, no)
AC_CHECK_PROGS(BIBTEX, bibtex, no)
AM_CONDITIONAL(HAVE_PDFLATEX, test $PDFLATEX != no)
AM_CONDITIONAL(HAVE_BIBTEX, test $BIBTEX != no)

# Checks for libraries.
AC_CHECK_LIB(m, main)
AC_CHECK_LIB(sundials_cvode, main,, AC_MSG_ERROR(could not find sundials/cvode library.))
AC_CHECK_LIB(sundials_nvecserial, main,, AC_MSG_ERROR(could not find sundials/nvecserial library.))
AC_CHECK_LIB(sundials_cvode, CVLapackDense,, AC_MSG_WARN(sundials was compiled without lapack.))
AX_LAPACK(,, AC_MSG_WARN(could not find lapack library.))
if test x$ac_cv_lib_sundials_cvode_CVLapackDense = xyes && test x$ax_lapack_ok = xyes; then
   have_lapack="yes"
   LIBS="$LAPACK_LIBS $BLAS_LIBS $LIBS $FLIBS"
else
   have_lapack="no"
fi  
AM_CONDITIONAL(HAVE_LAPACK, test x$have_lapack = xyes)

# Check for Python modules
AC_PYTHON_MODULE(numpy)
AC_PYTHON_MODULE(biggles)
if test $HAVE_PYMOD_NUMPY = "no"; then
   AC_MSG_WARN(could not find python module numpy.)
fi
if test $HAVE_PYMOD_BIGGLES = "no"; then
   AC_MSG_WARN(could not find python module biggles.)
fi
AM_CONDITIONAL([HAVE_PYMOD_BIGGLES], [test $HAVE_PYMOD_BIGGLES = "yes"])
AM_CONDITIONAL([HAVE_PYMOD_NUMPY], [test $HAVE_PYMOD_NUMPY = "yes"])

# Checks for typedefs, structures, and compiler characteristics.
if test x$enable_openmp = "xyes"; then
   AX_OPENMP(have_openmp="yes", have_openmp="no")
   AC_SUBST([OPENMP_CFLAGS])
else
   have_openmp="no"
fi

AC_CONFIG_FILES([Makefile
                 doc/Makefile
		 doc/astrochem.tex
		 doc/astrochem.1
		 doc/plabun.1
		 doc/plroute.1
		 doc/chmconvert.1
		 doc/examples/Makefile
		 lib/Makefile
		 lib/libastrochem.py
                 src/Makefile
		 src/chmconvert.py
		 src/plabun.py
		 src/plroute.py
		 networks/Makefile
		 tests/Makefile])

AC_OUTPUT

# Print configuration summary

echo "=============================================
Configuration summary:
C compiler:         ${CC} ${CFLAGS}
OpenMP support:     ${have_openmp}"
if test ${have_openmp} = "yes"; then
   echo "OpenMP flag:        ${OPENMP_CFLAGS}"
fi
echo "LAPACK support:     ${have_lapack}"
if test ${have_lapack} = "yes"; then
   echo "LAPACK version:     ${LAPACK_LIBS} ${BLAS_LIBS}"
fi
echo "Python interpreter: ${PYTHON}
Python version:     ${PYTHON_VERSION}
Numpy module:       ${HAVE_PYMOD_NUMPY}
Biggles module:     ${HAVE_PYMOD_BIGGLES}
============================================="