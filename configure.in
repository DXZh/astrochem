#  configure.in - Autoconf file for Astrochem
#
#  Copyright (c) 2006-2011 Sebastien Maret
# 
#  This file is part of Astrochem.
#
#  Astrochem is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published
#  by the Free Software Foundation, either version 3 of the License,
#  or (at your option) any later version.
#
#  Astrochem is distributed in the hope that it will be useful, but
#  WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
#  General Public License for more details.
# 
#  You should have received a copy of the GNU General Public License
#  along with Astrochem.  If not, see <http://www.gnu.org/licenses/>.

AC_PREREQ(2.59)
AC_INIT(astrochem, 0.1, sebastien.maret@obs.ujf-grenoble.fr)
AM_INIT_AUTOMAKE(astrochem, 0.1)
AC_CONFIG_SRCDIR([config.h.in])
AC_CONFIG_HEADER([config.h])

# Set package options
AC_ARG_ENABLE([openmp],	AC_HELP_STRING([--enable-openmp], [enable openmp (default is no)]))

# Check for programs
AC_PROG_CC
AM_PATH_PYTHON([2.4])
AC_CHECK_PROGS(PDFLATEX, pdflatex, no)
AC_CHECK_PROGS(BIBTEX, bibtex, no)
AM_CONDITIONAL(HAVE_PDFLATEX, test $PDFLATEX != no)
AM_CONDITIONAL(HAVE_BIBTEX, test $BIBTEX != no)

# Checks for libraries.
AC_CHECK_LIB(m, main)
AC_CHECK_LIB(sundials_cvode, main)
AC_CHECK_LIB(sundials_nvecserial, main)
if test $ac_cv_lib_sundials_cvode_main = "no"; then
   AC_MSG_ERROR(could not find SUNDIALS library.)
fi

# Check for Python modules
m4_include([m4/ac_python_module.m4])
AC_PYTHON_MODULE(numpy)
AC_PYTHON_MODULE(biggles)
if test $HAVE_PYMOD_NUMPY = "no"; then
   AC_MSG_WARN(could not find python module numpy.)
fi
if test $HAVE_PYMOD_BIGGLES = "no"; then
   AC_MSG_WARN(could not find python module biggles.)
fi
AM_CONDITIONAL([HAVE_PYMOD_BIGGLES], [test $HAVE_PYMOD_BIGGLES = "yes"])
AM_CONDITIONAL([HAVE_PYMOD_NUMPY], [test $HAVE_PYMOD_NUMPY = "yes"])

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
if test x$enable_openmp = "xyes"; then
   AX_OPENMP
   AC_SUBST([OPENMP_CFLAGS])
fi

# Checks for library functions.
AC_FUNC_MALLOC
AC_CHECK_FUNCS([pow exp fabs])

AC_CONFIG_FILES([Makefile
                 doc/Makefile
		 doc/examples/Makefile
                 src/Makefile
		 networks/Makefile
		 tests/Makefile])
AC_OUTPUT